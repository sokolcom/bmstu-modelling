    SIMULATE
    GENERATE  10,2,,300,  ;; блок GENERATE осуществляет ввод транзактов в модель
        ;  1  средний интервал времени между последовательными поступлениями транзактов в модель
        ; [2]  модификатор, который изменяет значения интервала генерации транзактов по сравнению 
            с интервалом, указанным операндом А
        ; [3]  задержка в выработке первого транзакта (0)
        ; [4]  число вырабатываемых источником заявок
        ; [5]  приоритет заявок
	
; если первый оператор занят, переход ко второму
M_OP1  GATE NU  POINT_OPER1,M_OP2  
        ;; блок GATE определяет состояние устройства
        ; оператор задает условие пропуска транзакта
        ;  NU  устройство не используется (NOT USED)
        ;  1  операнд задает устройство для проверки
        ; [2]  операнд задает блок, в который перейдет транзакт, если оператор вернет "FALSE" 

    SEIZE  POINT_OPER1  ;; транзакт занимает устройство
    ADVANCE  20,5  ;; задержка транзакта в течение некоторого времени
    RELEASE  POINT_OPER1  ;; освобождение устройства
    TRANSFER  ,M_PC1,,  ;; переход в блок первого компьютера

; если второй оператор занят, переход к третьему
M_OP2  GATE NU  POINT_OPER2,M_OP3   
    SEIZE  POINT_OPER2   ;; транзакт занимает устройство
    ADVANCE  40,10   ;; задержка транзакта
    RELEASE  POINT_OPER2   ;; устройство освобождается
    TRANSFER  ,M_PC1  ;; переход в блок первого компьютера

; если и третий оператор занят, заявка не обслуживается
M_OP3  GATE NU  POINT_OPER3,M_DROP  
    SEIZE  POINT_OPER3  
    ADVANCE  40,20  ;; задержка транзакта
    RELEASE  POINT_OPER3
    TRANSFER  ,M_PC2  ;; переход в блок второго компьютера

M_PC1  QUEUE  PC1_QUEUE  ;; постановка транзакта в очередь
    SEIZE  SPC1  ;; транзакт занимает устройство
    DEPART  PC1_QUEUE  ;; извлечение транзакта из очереди
    ADVANCE  15   ;; задержка транзакта
    RELEASE  SPC1  ;; освобождение устройства
    TRANSFER  ,M_PROC  ;; транзакт обслужен, переход к завершению 

M_PC2  QUEUE  PC2_QUEUE
    SEIZE  SPC2  
    DEPART  PC2_QUEUE
    ADVANCE  30  
    RELEASE  SPC2
    TRANSFER  ,M_PROC  ;; транзакт обслужен, переход к завершению 

M_PROC  TRANSFER  ,M_END
M_DROP  TRANSFER  ,M_END

; количество обработанных заявок
M_END  SAVEVALUE  TRANS_PROCESSED,N$M_PROC
    SAVEVALUE  TRANS_DROPPED,N$M_DROP
    ; вероятность потери заявки
    SAVEVALUE  TRANS_DROPPED_PROB,((N$M_DROP)/(N$M_END))

    TERMINATE  1
    START 300